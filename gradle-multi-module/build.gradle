plugins {
    //lombok插件
    id "io.freefair.lombok" version "5.3.0"
    //使用Spring依赖管理
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    //springboot
    id 'org.springframework.boot' version '2.4.2'
}
// ext 定义版本号
ext {
    //all project
    jupiter_api_version = '5.6.0'
    // api model
    mybatis_version = '2.1.4'
    swagger2_version = '2.9.2'
    swagger2_ui_version = '1.8.9'
    lombok_version = '1.18.12'
    // core project
    spring_context_support_version = '5.2.12.RELEASE'
    // pojo project
    swagger_annotations_version = '1.5.21'
    // utils project
    fastjson_version = '1.2.58'

}

//所有项目配置
allprojects {
    group 'com.multi'
    version '1.0-SNAPSHOT'
}

//子项目配置
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiter_api_version}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }
}

//api 模块配置
project(':multi-api') {
    apply plugin: "org.springframework.boot"
    apply plugin: "war"
    dependencies {
        implementation project(':multi-core')
        implementation project(':multi-pojo')
        implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatis_version}"
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.springframework.boot:spring-boot-starter-jdbc"
        implementation "io.springfox:springfox-swagger2:${swagger2_version}"
        implementation "com.github.xiaoymin:swagger-bootstrap-ui:${swagger2_ui_version}"
        implementation "org.projectlombok:lombok:${lombok_version}"
        runtimeOnly "mysql:mysql-connector-java"
        providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
    }
    jar {
        manifest {
            attributes 'Built-By': System.getProperty('user.name'),
                    'Build-Jdk': System.getProperty('java.version')
        }
    }
    war {
        manifest {
            attributes 'Built-By': System.getProperty('user.name'),
                    'Build-Jdk': System.getProperty('java.version')
        }
    }
}

//core 模块配置
project(':multi-core') {
    dependencies {
        implementation "org.springframework:spring-context-support:${spring_context_support_version}"
        implementation project(':multi-pojo')
        implementation project(':multi-utils')
    }
}

//pojo 模块配置
project(':multi-pojo') {
    dependencies {
        implementation "io.swagger:swagger-annotations:${swagger_annotations_version}"
    }
}

//工具 模块配置
project(':multi-utils') {
    dependencies {
        implementation "com.alibaba:fastjson:${fastjson_version}"
    }
}